(def {otherwise} true)
(def {e}  2.71828182845904523536)
(def {pi} 3.14159265358979323846)

; Function definitions.
(def {fun} (\ {args body} {
    def (head args) (\ (tail args) body)
}))

; Unback list for function.
(fun {unpack f xs} {
    eval (join (list f) xs)
})

; Pack list for function.
(fun {pack f & xs} {f xs})

; Curry and Uncurry
(def {uncurry} pack)
(def {curry} unpack)

; Assigner function
; (fun {let a = b} {def {a} b})

; Perform Several things in Sequence.
(fun {do & l} {
    if (== l nil)
        {nil}
        {last l}
})

; Open new scope
(fun {let b} {
    ((\ {_} b) ())
})

; Logical functions
(fun {not x} {! x})
(fun {inot x} { if (<= x 0) {false} {true} })
(fun {or x y} {|| x y})
(fun {and x y} {&& x y})

; List access
(fun {car l} { eval (head l) })
(fun {cdr l} { (tail l) })

(fun {select & cs} {
    if (== cs nil)
        {error "No Selection Found"}
        {if (fst (fst cs)) {snd (fst cs)} {unpack select (tail cs)}}
})

(fun {case x & cs} {
    if (== cs nil)
        {error "No Case Found"}
        {if (== x (fst (fst cs))) {snd (fst cs)}} {
            unpack case (join (list x) (tail cs))}
})

; Miscellaneous Functions
(fun {flip f a b} {f b a})
(fun {ghost & xs} {eval xs})
(fun {comp f g x} {f (g x)})

; First, Second, or Third Item in List
(fun {fst l} { eval (head l) })
(fun {snd l} { eval (head (tail l)) })
(fun {trd l} { eval (head (tail (tail l))) })

; List Length
(fun {len l} {
    if (== l nil)
        {0}
        {+ 1 (len (tail l))}
})

; Nth item in List
(fun {nth n l} {
    if (== n 0)
        {fst l}
        {nth (- n 1) (tail l)}
})

; Take N items
(fun {take n l} {
    if (== n 0)
        {nil}
        {join (head l) (take (- n 1) (tail l))}
})

; Drop N items
(fun {drop n l} {
    if (== n 0)
        {l}
        {drop (- n 1) (tail l)}
})

; Split at N
(fun {split n l} {list (take n l) (drop n l)})

; Element of List
(fun {elem x l} {
    if (== l nil)
        {false}
        {if (== x (fst l)) {true} {elem x (tail l)}}
})

; Find element in list of pairs {{a b}{c d}}
(fun {lookup x l} {
    if (== l nil)
        {error "No Element Found"}
        {do
            (= {key} (fst (fst l)))
            (= {val} (snd (fst l)))
            (if (== key x) {val} {lookup x (tail l)})
        }
})

; Zip two lists together into a list of pairs
(fun {zip x y} {
    if (or (== x nil) (== y nil))
        {nil}
        {join (list (join (head x) (head y))) (zip (tail x) (tail y))}
})

; Unzip a list of pairs into two list
(fun {unzip l} {
    if (== l nil)
        {{nil nil}}
        {do
            (= {x} (fst l))
            (= {xs} (unzip (tail l)))
            (list (join (head x) (fst xs)) (join (tail x) (snd xs)))
        }
})

; Merge lists
(fun {merge x y} {
    if (== x nil)
        {if (== y nil) {nil} {y} }
        {if (== y nil) {x}
            {join (join (head x) (head y)) (merge (tail x) (tail y))}
        }
})

; Last item in List
(fun {last l} {nth (- (len l) 1) l})

; Apply Function to List
(fun {map f l} {
    if (== l nil)
        {nil}
        {join (list (f (fst l))) (map f (tail l))}
})

; Apply Filter to List
(fun {filter f l} {
    if (== l nil)
        {nil}
        {join (if (f (fst l)) {head l} {nil}) (filter f (tail l))}
})

; Reverse List
(fun {reverse l} {
    if (== l nil)
        {nil}
        {join (reverse (tail l)) (head l)}
})

; Fold Left
(fun {foldl f z l} {
    if (== l nil) 
        {z}
        {foldl f (f z (fst l)) (tail l)}
})

; Fold Right
(fun {foldr f z l} {
    if (== l nil)
        {z}
        {f (fst l) (foldr f z (tail l))}
})

; Take While
(fun {take-while f l} {
    if (not (unpack f (head l)))
        {nil}
        {join (head l) (take-while f (tail l))}
})

; Drop While
(fun {drop-while f l} {
    if (not (unpack f (head l)))
        {l}
        {drop-while f (tail l)}
})

(fun {sum l} {foldl + 0 l})
(fun {product l} {foldl * 1 l})

; Factorial function
(fun {fact n} {
    if (== n 1)
        {1}
        {* n (fact (- n 1))}
})

; Square root
(fun {sqrt a} {
    if (< a 0)
        {error "Imaginary number"}
        {^ e (/ (ln a) 2)}
})

; Combinatorics
(fun {ncr n r} {
    if (> r n)
        {0}
        {/ (fact n) (* (fact r) (fact (- n r)))}
})

; Permutations
(fun {npr n r} {
    if (> r n)
        {0}
        {/ (fact n) (fact (- n r))}
})

; gcd
(fun {gcd a b} {
    if (== 0 (% a b))
        {b}
        {gcd b (% a b)}
})
; lcm
(fun {lcm a b} {/ (* a b) (gcd a b)})
; abs
(fun {abs a} {if (< a 0) {- a} {a}})
; mod
(fun {mod a b} {% a b})

; Small list conditional function
(fun {small l} { (or (== l nil) (== (len l) 1)) })
; Left half of a list
(fun {left-half l} {take (/ (len l) 2) l})
; Right half of a list
(fun {right-half l} {drop (/ (len l) 2) l})

; Maximum
(fun {maximum l} {
    if (== (len l) 1)
        {car l}
        {max (car l) (maximum(cdr l))}
})

; Minimum
(fun {minimum l} {
    if (== (len l) 1)
        {car l}
        {min (car l) (minimum(cdr l))}
})

; Range
(fun {range a b} {
    if (== a b)
        {list b}
        {if (< a b)
            {join (list a) (range (+ a 1) b)}
            {join (list a) (range (- a 1) b)}
        }
})

; Fibonacci
(fun {fib n} {
    select
        { (== n 0) 0 }
        { (== n 1) 1 }
        { otherwise (+ (fib (- n 1)) (fib (- n 2)))}
})
